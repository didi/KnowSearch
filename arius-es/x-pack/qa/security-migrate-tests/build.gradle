apply plugin: 'elasticsearch.standalone-rest-test'
apply plugin: 'elasticsearch.rest-test'

dependencies {
  testCompile project(xpackModule('core'))
  testCompile project(path: xpackModule('security'), configuration: 'runtime')
  testCompile project(path: xpackProject('transport-client').path, configuration: 'runtime')
}

testClusters.integTest {
  testDistribution = "DEFAULT"
  setting 'xpack.security.enabled', 'true'
  setting 'xpack.license.self_generated.type', 'trial'
  extraConfigFile 'roles.yml', file('roles.yml')
  [
    test_admin: 'superuser',
    transport_user: 'superuser',
    existing: 'superuser',
    bob: 'actual_role'
  ].each { String username, String role ->
    user username: username, password: 'x-pack-test-password', role: role
  }
}

integTest.runner {
  nonInputProperties.systemProperty 'tests.config.dir', "${-> testClusters.integTest.singleNode().getConfigDir()}"
}

testingConventions {
  naming.clear()
  naming {
    IT {
      baseClass 'org.elasticsearch.xpack.security.MigrateToolTestCase'
    }
  }
}
